#include "native/PREFIX_SUM"
#include "native/CLUSTERED_LIGHT_SHADING"
#include "native/SAMPLE_COUNT"

#include "../common.glsl"
#include "cluster_space_buffer.glsl"
#include "cluster_fragment_counts_buffer.glsl"

layout(binding = DEPTH_SAMPLER_LOC) uniform sampler2DMS depth_sampler;
// layout(location = FB_DIMS_LOC) uniform vec2 fb_dims;
layout(location = REN_CLP_TO_CLU_CLP) uniform mat4 ren_clp_to_clu_clp;

#if SAMPLE_COUNT == 1
layout(local_size_x = 16, local_size_y = 16, local_size_z = SAMPLE_COUNT) in;
#elif SAMPLE_COUNT == 2
layout(local_size_x =  8, local_size_y = 16, local_size_z = SAMPLE_COUNT) in;
#elif SAMPLE_COUNT == 4
layout(local_size_x =  8, local_size_y =  8, local_size_z = SAMPLE_COUNT) in;
#elif SAMPLE_COUNT == 8
layout(local_size_x =  4, local_size_y =  8, local_size_z = SAMPLE_COUNT) in;
#elif SAMPLE_COUNT == 16
layout(local_size_x =  4, local_size_y =  4, local_size_z = SAMPLE_COUNT) in;
#else
#error Unsupported SAMPLE_COUNT.
#endif

void main() {
  ivec2 depth_dims = textureSize(depth_sampler);

  if (any(greaterThanEqual(gl_GlobalInvocationID.xy, depth_dims))) {
    // Out of texture bounds.
    return;
  }

  // pos_in_ndc is known.
  vec2 xy_ndc = vec2(ivec2(gl_GlobalInvocationID.xy) * 2 + 1 - depth_dims) / vec2(depth_dims);
  float z_ndc = texelFetch(depth_sampler, ivec2(gl_GlobalInvocationID.xy), int(gl_LocalInvocationID.z)).r;

  if (z_ndc == 0) {
    // Infinitely far (reverse z)
    return;
  }

  vec3 pos_in_cls = from_homogeneous(ren_clp_to_clu_clp * to_homogeneous(vec3(xy_ndc, z_ndc)));

  uvec3 idx_in_cls = uvec3(pos_in_cls);
  if (all(lessThan(idx_in_cls, cluster_space.dimensions))) {
    uint cluster_index = index_3_to_1(idx_in_cls, cluster_space.dimensions);
    atomicAdd(cluster_fragment_counts[cluster_index], 1);
  }
}
