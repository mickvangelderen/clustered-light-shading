#include "../common.glsl"

#include "cluster_space_buffer.glsl"
#include "compute_commands_buffer.glsl"
#include "active_cluster_light_counts_buffer.glsl"
#include "active_cluster_cluster_indices_buffer.glsl"
#include "cluster_fragment_counts_buffer.glsl"
// out
#include "profiling_cluster_buffer.glsl"

// NOTE(mickvangelderen): This whole shader expects local_size_z = 1
// NOTE(mickvangelderen): We assume local_size_x equals the number of bins in the output histogram.
layout(local_size_x = 32, local_size_y = 8) in;

#define LOCAL_SIZE   (gl_WorkGroupSize.x * gl_WorkGroupSize.y)
#define LOCAL_OFFSET  (gl_LocalInvocationID.y * gl_WorkGroupSize.x)
#define LOCAL_INDEX  (LOCAL_OFFSET + gl_LocalInvocationID.x)
#define GLOBAL_OFFSET (gl_WorkGroupID.x * LOCAL_SIZE)
#define GLOBAL_INDEX (GLOBAL_OFFSET + LOCAL_INDEX)

shared uint shared_light_counts[LOCAL_SIZE];
shared uint shared_fragment_counts[LOCAL_SIZE];

uint bin(uint value) {
  return min(gl_WorkGroupSize.x - 1, value / 8);
}

void main() {
  uint item_count = compute_commands[COMPUTE_COMMAND_INDEX_ACTIVE_CLUSTER_COUNT].work_group_x;
  bool within_bounds = GLOBAL_INDEX < item_count;

  // Read a block of data in one go.
  shared_light_counts[LOCAL_INDEX] = within_bounds ? active_cluster_light_counts[GLOBAL_INDEX] : 0;
  shared_fragment_counts[LOCAL_INDEX] = within_bounds ? cluster_fragment_counts[active_cluster_cluster_indices[GLOBAL_INDEX]] : 0;
  memoryBarrierShared();
  barrier();

  // Have every thread go over up to local_size_x values
  uint local_end = min(LOCAL_OFFSET + gl_WorkGroupSize.x, item_count - GLOBAL_OFFSET);
  uint clus_acc = 0;
  uint frag_acc = 0;
  for (uint i = LOCAL_OFFSET; i < local_end; i++) {
    uint value = shared_light_counts[i];
    if (bin(value) == gl_LocalInvocationID.x) {
      clus_acc += 1;
      frag_acc += shared_fragment_counts[i];
    }
  }

  atomicAdd(profiling_cluster_buffer.lights_per_cluster_hist[gl_LocalInvocationID.x], clus_acc);
  atomicAdd(profiling_cluster_buffer.lights_per_fragment_hist[gl_LocalInvocationID.x], frag_acc);
}
